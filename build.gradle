plugins {
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    // 可以在 dependencies 中使用 api 类型. 有关 api 和 implementation 的区别:
    // https://docs.gradle.org/current/userguide/java_library_plugin.html
    id 'java-library'
    id 'jacoco'
    id 'maven-publish'
}

group = 'ink.wulian.er'
version = '0.1.1'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    maven { url 'https://maven.aliyun.com/nexus/content/groups/public/' }
    maven { url "https://jitpack.io" }
    mavenCentral()
    maven { url 'https://repo.spring.io/milestone' }
    maven { url 'https://nexus.5lian.ink/repository/the-lands-between/' }
}

dependencies {
    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // springboot
    api 'org.springframework.boot:spring-boot-starter'
    api 'org.springframework.boot:spring-boot-starter-web'

    // springboot actuator
    api 'org.springframework.boot:spring-boot-starter-actuator'

    api 'ink.wulian.er:godrick:1.0.0'
    api group: 'cn.hutool', name: 'hutool-all', version: '5.7.13'

    implementation group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: '2.7.4'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter', version: '2.7.4'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

java {
    withSourcesJar()
}

tasks.named('test') {
    useJUnitPlatform()
}

publishing {

    repositories {
        maven {
            url 'https://nexus.5lian.ink/repository/the-lands-between/'
            credentials {
                username 'dev-0'
                password 'todo'
            }
        }
    }

    publications {
        mavenJava(MavenPublication) {
            artifactId = rootProject.name
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Malenia'
                description = 'A Web Server Infra using SpringBoot.'
                url = 'https://nexus.5lian.ink'
            }
        }
    }

}